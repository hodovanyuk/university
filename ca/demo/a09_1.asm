; Пример 9.1. Вывод на экран графического изображения с помощью прерывания BIOS 10h

text    segment                 ;(1) начало сегмента команд
assume  CS:text                 ;(2)

begin:  
; установим графический видеорежим
        mov AH,0h               ;(1) функция установки видеорежима
        mov AL,10h              ;(2) графический режим 16 цветов
        int 10h                 ;(3) вызов BIOS
; выведем на экран желтый прямоугольник
        mov AH,0Ch              ;(4) функция вывода пиксела
        mov AL,0Eh              ;(5) желтый цвет
        mov BH,0                ;(6) видеостраница
        mov CX,50               ;(7) начальная х-координата
c2:     mov DX,10               ;(8) начальная у-координата
c1:     int 10h                 ;(9) вызов BIOS - вывод точки
        inc DX                  ;(10) инкремент по у
        cmp DX,330              ;(11) дошли до границы по у?
        jne c1                  ;(12) нет, повторяем вывод точек
        inc CX                  ;(13) дошли до границы по у, инкремент по х
        cmp CX,610              ;(14) дошли до границы по х?
        jne c2                  ;(15) нет, повторяем вывод вертикальных линий

; остановим программу для наблюдения результата
        mov AH,01h              ;(16) дошли до границы по х, останов
        int 21h                 ;(17) функцией ввода символа с клавиатуры

; переведем систему назад в текстовый режим
        mov AX,3                ;(18) восстановим текстовый
        int 10h                 ;(19) видеорежим

; завершим программу
        mov AX,4C00h            ;(20)
        int 21h                 ;(21)

text    ends                    ;(22) конец сегмента команд

stk     segment stack           ;(23) начало сегмента стека
        db 256 dup (0)          ;(24) стек
stk     ends                    ;(25) конец сегмента стека

end begin                       ;(26) конец текста программы
