; Пример 9.2. Чтение и сохранение в файле главного загрузчика жесткого диска

text    segment                 ;( ) начало сегмента команд
assume  CS:text,DS:data         ;( )

begin:  mov AX,data             ;( ) инициализация сегментного
        mov DS,AX               ;( )    регистра DS

; чтение главного загрузчика
        mov AH,02h              ;(1) функция чтения секторов
        mov AL,1                ;(2) прочитаем 1 сектор
        mov CH,0                ;(3) номер цилиндра
        mov CL,1                ;(4) номер сектора
        mov DH,0                ;(5) номер поверхности
        mov DL,80h              ;(6) код жесткого диска
        mov BX,offset mboot     ;( ) буфер для прочитанного
        int 13h                 ;(7) переход в BIOS

; создадим файл для сохранения прочитанного
        mov AH,3Ch              ;(8) функция (DOS) создания файла
        mov CX,0                ;(9) файл будет без атрибутов
        mov DX,offset fname     ;(10) смещение имени файла
        int 21h                 ;(11) переход в DOS
        mov BX,AX               ;(12) сохраним в ВХ дескриптор файла

; запишем в файл данные из буфера mboot
        mov AH,40h              ;(13) функция записи в файл
        mov CX,512              ;(14) число выводимых байтов
        mov DX,offset mboot     ;(15) смещение буфера
        int 21h                 ;(16) переход в DOS

; завершим программу
        mov AX,4C00h            ;( )
        int 21h                 ;( )
text    ends                    ;( ) конец сегмента команд

data    segment                 ;( ) начало сегмента данных
; поля данных (в сегменте данных)
mboot   db 512 dup(0)           ;(17)
fname   db 'mboot.dat',0        ;(18)
data    ends                    ;(22) конец сегмента данных

stk     segment stack           ;(23) начало сегмента стека
        db 256 dup (0)          ;(24) стек
stk     ends                    ;(25) конец сегмента стека

end begin                       ;(26) конец текста программы
