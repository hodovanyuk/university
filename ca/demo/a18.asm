
; Пример 18.1. Ввод с клавиатуры последовательности 16-ричных чисел
; с выводом их в дисковый файл

; в сегменте команд

; подпрограмма ввода чисел
hexin   proc
        mov BX,0                ; очистим вспомогательный регистр
inpt:   mov AH,08h              ; функция ввода символа
        int 21h
        cmp AL,13               ; клавиша Enter?
        je done                 ; да, на вывод пробела
        cmp AL,27               ; клавиша Esc?
        je done1                ; да, на выход из подпрограммы
        cmp AL,'0'              ; меньше '0'?
        jb inpt                 ; да, на повторение ввода
        cmp AL,'9'              ; не больше '9'?
        jbe ok                  ; да, введена цифра
        cmp AL,'F'              ; больше 'F'?
        ja inpt                 ; да, на повторение ввода
        cmp AL,'А'              ; меньше 'А'?
        jb inpt                 ; да, на повторение ввода
ok:     mov AH,02h              ; введено разумное,
        mov DL,AL               ; выведем символ на экран
        int 21h
        cmp AL,'9'              ; введена цифра?
        ja letter               ; буква!
        sub AL,'0'              ; введена цифра, преобразуем в число
        and AX,0Fh              ; замаскируем остальные биты
        jmp addd                ; на продолжение
letter: sub AL,55               ; введена буква, преобразуем в число
        and AX,0Fh              ; замаскируем остальные биты
addd:   mov CL,4                ; умножим предыдущий результат
        sal BX,CL               ; на 16 сдвигом на 4 бита влево
        or BX,AX                ; добавим новую цифру
        jmp inpt                ; будем вводить дальше
done:   mov AH,02h              ; выведем пробел
        mov DL,' '              ; между вводимыми числами
        int 21h
done1:  ret                     ; в BX введенное число
hexin   endp

; фрагмент главной процедуры
; создадим файл на диске
        mov AH,3Ch              ; функция создания файла
        mov CX,0                ; без атрибутов
        mov DX,offset fname     ; адрес имени файла
        int 21h
        mov handle,AX           ; сохраним дескриптор
; вызов в цикле процедуры ввода 16-ричного числа
write:  call hexin              ; ввод числа
        cmp AL,27               ; вернулся код Esc?
        je close                ; да, на завершение
        mov buf,BX              ; перенесем результат в память
        mov AH,40h              ; функция записи в файл
        mov BX,handle           ; дескриптор файла
        mov CX,2                ; запись 2 байт
        mov DX,offset buf       ; адрес буфера вывода
        int 21h
        jmp write               ; на ввод следующего числа
close:  mov AH,09h              ; выведем завершающее сообщение
        mov DX,offset mesg
        int 21h

; в сегменте данных
fname   db 'nums.dat',0         ; имя файла
handle  dw 0                    ; ячейка для дескриптора
buf     dw 0                    ; буфер вывода
mesg    db 'Формирование файла завершено$'
